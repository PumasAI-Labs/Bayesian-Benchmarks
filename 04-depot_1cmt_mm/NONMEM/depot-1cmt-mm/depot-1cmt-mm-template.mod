;Model Desc: 04-depot-1cmt-mm
;Project Name: Bayesian Benchmarks

$PROB 
$INPUT ID	AMT	RATE	II	ADDL	CMT	EVID	SS	LLOQ	BLOQ	MDV	TIME	ODV DV

$DATA ../../../data/single_dose.csv IGNORE=@

$SUBROUTINE ADVAN13 TOL=9

$MODEL
NCOMP=2
COMP=(GUT, DEFDOSE)
COMP=(CENT)

$PK 
MU_1=THETA(1)
MU_2=THETA(2)
MU_3=THETA(3)
MU_4=THETA(4)

V=EXP(MU_1+ETA(1))
VMAX=EXP(MU_2+ETA(2))
KM=EXP(MU_3+ETA(3))
KA=EXP(MU_4+ETA(4))

S2=V

$DES
CP=A(2)/V

DADT(1) = -KA*A(1)
DADT(2) = KA*A(1) - VMAX*CP/(KM+CP)

$ERROR
IPRED=LOG(F+1E-16)
Y=IPRED+EPS(1)

; Initial Estimates  
$THETA
THETAUPDATE

$OMEGA BLOCK(4)
OMEGAUPDATE

$SIGMA 
SIGMAUPDATE

; Prior Specification
$PRIOR NWPRI

$THETAP 4.248 FIX 0 FIX -1.386 FIX 0 FIX
$THETAPV BLOCK(4) 
1 FIX
0 1
0 0 1
0 0 0 1

$OMEGAP BLOCK(4) 
0.16  FIX
0 0.16
0 0 0.16
0 0 0 0.16
$OMEGAPD 4 FIX

$SIGMAP 0.25 FIX
$SIGMAPD 1 FIX

$EST METHOD=NUTS 
INTERACTION
AUTO=1
PRINT=50 
NBURN=500 
NITER=1000 
OLKJDF=2 
SEED=12345
FILE=ITERFILE

$COV UNCONDITIONAL MATRIX=R PRINT=E

$TABLE ID EVID TIME IPRED NOPRINT ONEHEADER FORMAT=,1PE13.6 FILE=SDFILE
$TABLE ID V VMAX KM KA ETA1 ETA2 ETA3 ETA4 NOPRINT NOAPPEND ONEHEADER FORMAT=,1PE13.6 FILE=PAFILE
